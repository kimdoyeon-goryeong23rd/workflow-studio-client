plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.saltlux'
version = '0.1.0'
description = 'Java sdk library for Saltlux Workflow Studio deepresearch'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.5.9'
    }
}

dependencies {
    implementation 'com.github.agent-hanju:content-stream-adapter:0.1.3'

    api 'org.springframework:spring-webflux'
    api 'com.fasterxml.jackson.core:jackson-databind'
    api 'org.slf4j:slf4j-api'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.springframework:spring-context'
    testImplementation 'io.projectreactor.netty:reactor-netty-http'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'org.slf4j:slf4j-simple'
}
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
}

// 통합 테스트 태스크 - 수동 실행용
tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    useJUnitPlatform {
        includeTags 'integration'
    }
    shouldRunAfter tasks.named('test')
}


publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/kimdoyeon-goryeong23rd/workflow-studio-client'

                licenses {
                    license {
                        name = 'Saltlux Proprietary License'
                        url = 'https://github.com/kimdoyeon-goryeong23rd/workflow-studio-client/blob/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'kimdoyeon-goryeong23rd'
                        name = 'Kim Doyeon'
                        email = 'doyeon.kim2@saltlux.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/kimdoyeon-goryeong23rd/workflow-studio-client.git'
                    developerConnection = 'scm:git:ssh://github.com/kimdoyeon-goryeong23rd/workflow-studio-client.git'
                    url = 'https://github.com/kimdoyeon-goryeong23rd/workflow-studio-client'
                }
            }
        }
    }
}
